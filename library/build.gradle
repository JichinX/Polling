plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation("androidx.activity:activity-ktx:1.2.0-beta01")
    implementation "androidx.fragment:fragment-ktx:1.3.0-beta01"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
//    implementation "androidx.startup:startup-runtime:1.0.0"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation "androidx.lifecycle:lifecycle-process:2.3.0-beta01"
    implementation 'com.google.android.material:material:1.2.1'
}
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError = false
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
def libVersion = '1.0.0-SNAPSHOT'
afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            aar(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release
                artifact sourcesJar
                artifact javadocJar

                // You can then customize attributes of the publication as shown below.
                groupId = 'me.xujichang.lib'
                artifactId = rootProject.name.toLowerCase()
                version = libVersion
            }
        }
        repositories {
            maven {
                name = "company"
                url = libVersion.toLowerCase().endsWith('snapshot') ? COMPANY_SNAPSHOT : COMPANY_RELEASE
                credentials {
                    username = 'xujichang'
                    password = 'hsl521xjc'
                }
            }
        }
    }
}
